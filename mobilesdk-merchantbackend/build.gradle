plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 30

    defaultConfig {
        minSdk 21
        targetSdk 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "proguard-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation project(':mobilesdk')
    testImplementation project(':testcommon')
    androidTestImplementation project(':testcommon')

    implementation libs.'kotlinx-coroutines-core'
    implementation libs.'kotlinx-coroutines-android'

    debugImplementation libs.'fragment-testing'

    implementation libs.'play-services-base'
    implementation libs.'play-services-basement'

    implementation libs.gson

    implementation libs.okhttp

    testImplementation libs.'kotlin-reflect'
    testImplementation libs.'androidx-test-junit'
    testImplementation libs.junit
    testImplementation libs.robolectric
    testImplementation libs.'mockito-inline'
    testImplementation libs.'mockito-kotlin'
    testImplementation libs.mockwebserver

    androidTestImplementation libs.'androidx-test-core'
    androidTestImplementation libs.'androidx-test-runner'
    androidTestImplementation libs.uiautomator
    androidTestImplementation libs.junit
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    from dokkaJavadoc.outputDirectory
    archiveClassifier.set('javadoc')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact sourceJar
                artifact javadocJar

                pom {
                    name = "$groupId:$artifactId"
                    description = 'Utilities for interfacing the Swedbank Pay Android SDK with a Merchant Backend API backend'
                    url = 'https://developer.swedbankpay.com/modules-sdks/mobile-sdk/'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            name = 'Swedbank Pay'
                            email = 'opensource@swedbankpay.com'
                            organization = 'Swedbank Pay'
                            organizationUrl = 'https://www.swedbankpay.com/'
                        }
                        developer {
                            name = 'Pertti Kr√∂ger'
                            email = 'pertti@qvik.fi'
                            organization = 'Qvik'
                            organizationUrl = "https://qvik.com/"
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/SwedbankPay/swedbank-pay-sdk-android.git'
                        developerConnection = 'scm:git:ssh://github.com:SwedbankPay/swedbank-pay-sdk-android.git'
                        url = 'https://github.com/SwedbankPay/swedbank-pay-sdk-android'
                    }
                }
            }
        }
    }

    signing {
        def signingKey = System.getenv('SIGNING_KEY')
        def signingKeyPassword = System.getenv('SIGNING_KEY_PASSWORD')
        if (signingKey && signingKeyPassword) {
            def signingKeyId = System.getenv('SIGNING_KEY_ID')
            if (signingKeyId?.isEmpty()) signingKeyId = null
            useInMemoryPgpKeys(signingKeyId, signingKey, signingKeyPassword)
            sign publishing.publications.release
        }
    }
}
